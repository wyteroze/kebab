// Copyright 2026 wyteroze. Licensed under the Apache License, Version 2.0.

const std = @import("std");
const zlua = @import("zlua");
const shared = @import("shared/shared.zig");
const types = @import("../types.zig");
const MeshData = @import("../MeshData.zig").MeshData;
const ImageData = @import("../ImageData.zig").ImageData;
const Lua = zlua.Lua;
var allocator: std.mem.Allocator = undefined;
var io: std.Io = undefined;

const mesh_path = "src/assets/models/";
const image_path = "src/assets/images/";
const assets_lib = [_]zlua.FnReg {
    .{ .name = "loadMesh", .func = zlua.wrap(loadMesh) },
    .{ .name = "loadImage", .func = zlua.wrap(loadImage) }
};

pub fn loadMesh(l: *Lua) i32 {
    const path = l.checkString(1);
    const mesh_data = l.newUserdata(MeshData, 0);
    const full_path = std.mem.concat(allocator, u8, &.{ mesh_path, path }) catch |e| {
        l.raiseErrorStr("failed to form full path from '%s': '%s'", .{ path.ptr, @errorName(e).ptr });
        return 0;
    };
    defer allocator.free(full_path);

    mesh_data.* = MeshData.loadFromFile(allocator, io, full_path) catch |e| {
        l.raiseErrorStr("failed to load mesh_data '%s': '%s'", .{ full_path.ptr, @errorName(e).ptr });
        return 0;
    };

    l.setMetatableRegistry("MeshData");
    return 1;
}

pub fn loadImage(l: *Lua) i32 {
    const path = l.checkString(1);
    const img_data = l.newUserdata(ImageData, 0);
    const full_path = std.mem.concat(allocator, u8, &.{ image_path, path }) catch |e| {
        l.raiseErrorStr("failed to form full path from '%s': '%s'", .{ path.ptr, @errorName(e).ptr });
        return 0;
    };
    defer allocator.free(full_path);

    img_data.* = ImageData.loadFromFile(allocator, io, full_path) catch |e| {
        l.raiseErrorStr("failed to load image '%s': '%s'", .{ full_path.ptr, @errorName(e).ptr });
        return 0;
    };

    l.setMetatableRegistry("ImageData");
    return 1;
}

fn meshDataGc(l: *Lua) i32 {
    const mesh_data = l.checkUserdata(MeshData, 1, "MeshData");
    mesh_data.deinit();
    return 0;
}

fn imageDataGc(l: *Lua) i32 {
    const img_data = l.checkUserdata(ImageData, 1, "ImageData");
    img_data.deinit(allocator);
    return 0;
}

pub fn register(l: *Lua, a: std.mem.Allocator, i: std.Io) !void {
    allocator = a;
    io = i;

    // Assets library
    l.newTable();
    l.setFuncs(&assets_lib, 0);
    l.setGlobal("Assets");

    // Datatypes
    try shared.registerSimpleClass(l, "MeshData", zlua.wrap(meshDataGc));
    try shared.registerSimpleClass(l, "ImageData", zlua.wrap(imageDataGc));
}
