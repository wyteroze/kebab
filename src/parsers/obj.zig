// Copyright 2026 wyteroze. Licensed under the Apache License, Version 2.0.

const std = @import("std");
const types = @import("../types.zig");
const log = @import("../log.zig").obj;
const Mesh = @import("../Mesh.zig").Mesh;

const Vec2_SIMD = types.Vec2_SIMD;
const Vec3_SIMD = types.Vec3_SIMD;
const Vertex = types.Vertex;
const Face = types.Face;

pub const ParseError = error{
    MissingComponents,
    InvalidFaceData,
    InvalidFloat,
    InvalidInt
};

pub fn parseObj(allocator: std.mem.Allocator, reader: *std.Io.Reader) !Mesh {
    log.debug("parsing obj", .{});

    var raw_positions = std.ArrayList(Vec3_SIMD).empty;
    defer raw_positions.deinit(allocator);
    var raw_uvs = std.ArrayList(Vec2_SIMD).empty;
    defer raw_uvs.deinit(allocator);

    var vertices = std.ArrayList(Vertex).empty;
    defer vertices.deinit(allocator);
    var indices = std.ArrayList(usize).empty;
    defer indices.deinit(allocator);
    var faces = std.ArrayList(Face).empty;
    defer faces.deinit(allocator);

    while (try reader.takeDelimiter('\n')) |line| {
        if (std.mem.startsWith(u8, line, "#")) {
            continue; // comment
        } else if (std.mem.startsWith(u8, line, "vn")) {
            continue; // we don't care about normals
        } else if (std.mem.startsWith(u8, line, "vt")) {
            const uv = try parseTextureLine(line);
            try raw_uvs.append(allocator, uv);
        } else if (std.mem.startsWith(u8, line, "v ")) {
            const vertex = try parseVertexLine(line);
            try raw_positions.append(allocator, vertex);
        } else if (std.mem.startsWith(u8, line, "f ")) {
            const start_idx = indices.items.len;

            const tri_count = try parseFaceLine(allocator, line, raw_positions.items, raw_uvs.items, &vertices, &indices);
            try faces.append(allocator, .{
                .start = start_idx,
                .length = tri_count * 3
            });
        }
    }

    log.info("parsed obj: {d} vertices, {d} faces", .{ vertices.items.len, faces.items.len });
    return Mesh.init(
        allocator,
        vertices.items,
        indices.items,
        faces.items,
        null // objs do not store textures
    );
}

fn parseVertexLine(line: []const u8) ParseError!Vec3_SIMD {
    var iter = std.mem.splitScalar(u8, line, ' ');
    _ = iter.next(); // consume "v"

    var vertex: [3]f32 = undefined;
    var index: usize = 0;
    while (iter.next()) |component| {
        if (component.len == 0) continue;
        if (index >= 3) break;

        vertex[index] = std.fmt.parseFloat(f32, component)
            catch return ParseError.InvalidFloat;

        index += 1;
    }

    if (index < 3) {
        log.warn("vertex line missing components: '{s}'", .{ line });
        return ParseError.MissingComponents;
    }

    return Vec3_SIMD{ vertex[0], vertex[1], vertex[2] };
}

fn parseFaceLine(allocator: std.mem.Allocator,
    line: []const u8,
    raw_positions: []const Vec3_SIMD,
    raw_uvs: []const Vec2_SIMD,
    vertices: *std.ArrayList(Vertex),
    indices: *std.ArrayList(usize)
) !usize {
    var space_iter = std.mem.splitScalar(u8, line, ' ');
    _ = space_iter.next(); // consume "f"

    var corner_count: usize = 0;
    var corner_indices: [32]usize = undefined;

    while (space_iter.next()) |corner_str| {
        if (corner_str.len == 0) continue;
        if (corner_count >= corner_indices.len) {
            log.warn("face has more corners than supported ({d}). truncating: '{s}'", .{ corner_indices.len, line });
            break;
        }

        var slash_iter = std.mem.splitScalar(u8, corner_str, '/');

        // position index
        const v_str = slash_iter.next()
            orelse return ParseError.InvalidFaceData;

        const v_idx = std.fmt.parseInt(usize, v_str, 10)
            catch return ParseError.InvalidInt;

        if (v_idx == 0 or v_idx > raw_positions.len) {
            log.warn("face references out of range vertex index {d} (max is {d})", .{ v_idx, raw_positions.len });
            return ParseError.InvalidFaceData;
        }
        const pos = raw_positions[v_idx - 1];

        // uv index
        var uv: Vec2_SIMD = Vec2_SIMD{ 0.0, 0.0 };
        if (slash_iter.next()) |vt_str| {
            if (vt_str.len > 0) {
                const vt_idx = std.fmt.parseInt(usize, vt_str, 10)
                    catch return ParseError.InvalidFaceData;

                if (vt_idx == 0 or vt_idx > raw_uvs.len) {
                    log.warn("face references out-of-range uv index {d} (max is {d})", .{ vt_idx, raw_uvs.len });
                    return ParseError.InvalidFaceData;
                }

                uv = raw_uvs[vt_idx - 1];
            }
        }

        // vertex
        const new_vertex = Vertex{ .position = pos, .uv = uv };
        try vertices.append(allocator, new_vertex);
        corner_indices[corner_count] = vertices.items.len - 1;

        corner_count += 1;
    }

    if (corner_count < 3) {
        log.warn("face line has fewer than 3 corners: '{s}'", .{line});
        return ParseError.InvalidFaceData;
    }

    // triangulate
    var tri_count: usize = 0;
    var i: usize = 1;
    while (i + 1 < corner_count) : (i += 1) {
        try indices.append(allocator, corner_indices[0]);
        try indices.append(allocator, corner_indices[i]);
        try indices.append(allocator, corner_indices[i + 1]);
        tri_count += 1;
    }

    return tri_count;
}


fn parseTextureLine(line: []const u8) ParseError!Vec2_SIMD {
    var iter = std.mem.splitScalar(u8, line, ' ');
    _ = iter.next(); // consume "vt"

    var uv: [2]f32 = undefined;
    var index: usize = 0;
    while (iter.next()) |component| {
        if (component.len == 0) continue;
        if (index >= 2) break; // ignore w component

        uv[index] = std.fmt.parseFloat(f32, component)
            catch return ParseError.InvalidFloat;

        index += 1;
    }

    if (index < 2) {
        log.warn("texture line has fewer than 2 components: '{s}'", .{line});
        return ParseError.MissingComponents;
    }
    return Vec2_SIMD{ uv[0], 1.0 - uv[1] }; // flip V
}
